
import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.8'
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:2.2.1.RELEASE'
        classpath 'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:5.1.0.4882'
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.google.protobuf'
apply plugin: 'jacoco'
apply plugin: 'org.sonarqube'

sourceCompatibility = 11
targetCompatibility = 11

ext {
    activemqVersion = '5.12.0'
}

configurations {
    compile.exclude module: 'spring-boot-starter-logging'
}

repositories {
    mavenCentral()
    jcenter()
}

// Spring dependency versions are managed by the io.spring.dependency-management plugin.
// Appropriate versions will be pulled based on the spring boot version specified in the
// spring-boot-gradle-plugin.
dependencies {
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.6'
    compile group: 'com.google.guava', name: 'guava', version: '26.0-android'
    compile group: 'com.sun.mail', name: 'mailapi', version: '1.5.4'
    compile group: 'commons-lang', name: 'commons-lang', version: '2.6'
    compile group: 'io.grpc', name: 'grpc-all', version: '1.47.0'
    compile group: 'org.apache.activemq', name: 'activemq-pool', version: activemqVersion
    compile group: 'org.apache.velocity', name: 'velocity', version: '1.7'
    compile group: 'org.jdom', name: 'jdom', version: '1.1.3'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-jdbc'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    compile group: 'org.springframework', name: 'spring-context-support'
    compile group: 'org.springframework', name: 'spring-jms'
    compile group: 'org.quartz-scheduler', name: 'quartz', version: '2.2.1', { exclude group: 'c3p0', module: 'c3p0' }
    compile group: 'org.postgresql', name: 'postgresql', version: '42.2.2'
    compile group: 'com.google.protobuf', name: 'protobuf-java', version: '3.21.2'
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.16.0'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.16.0'
    compile group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.16.0'
    compile group: 'io.sentry', name: 'sentry-log4j2', version: '7.11.0'
    compile group: 'io.prometheus', name: 'simpleclient', version: '0.16.0'
    compile group: 'io.prometheus', name: 'simpleclient_servlet', version: '0.16.0'

    protobuf fileTree("../proto/")

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test'
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.8.0'
    testCompile group: 'io.zonky.test', name: 'embedded-postgres', version: '1.3.1'
    testCompile group: 'org.flywaydb', name: 'flyway-core', version: '5.2.0'

    // Use newer version of Postgres for tests: https://github.com/zonkyio/embedded-postgres/issues/78
    implementation enforcedPlatform('io.zonky.test.postgres:embedded-postgres-binaries-bom:11.13.0')
}

compileJava {
    options.compilerArgs << "-Xlint:all" << "-Werror"
}

compileTestJava {
    options.compilerArgs << "-Xlint:all" << "-Werror"
}

protobuf {
    protoc {
        // The protoc compiler
        artifact = 'com.google.protobuf:protoc:3.21.2'
    }
    plugins {
        grpc {
            // Generate gRPC stubs.
            artifact = 'io.grpc:protoc-gen-grpc-java:1.47.0'
        }
    }
    generateProtoTasks {
        ofSourceSet('main')*.plugins {
            // Apply the "grpc" plugin to all sources.
            grpc { }
        }
    }
    generatedFilesBaseDir = "${projectDir}/src/compiled_protobuf"
}

// Help IDEs recognize the generated code.
sourceSets {
    main {
        java {
            srcDirs += 'src/compiled_protobuf/main/java'
            srcDirs += 'src/compiled_protobuf/main/grpc'
        }
    }
    test {
        resources.srcDirs = ["src/test/resources"]
    }
}

bootJar {
    baseName = 'cuebot'
}

jacoco {
    toolVersion = "0.8.12"
}

jacocoTestReport {
    reports {
        // XML is used by SonarCloud. HTML is generated if you want to generate a human-readable
        // report locally. Reports are placed in build/reports/jacoco/.
        xml.enabled true
        html.enabled true
    }

    // Exclude files from test coverage.
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: [
                            // Exclude proto files' generated Java code.
                            'com/imageworks/spcue/grpc/**'])
        }))
    }
}

sonarqube {
    properties {
        property "sonar.java.source", "11"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.organization", "academysoftwarefoundation"
        property "sonar.projectKey", "AcademySoftwareFoundation_OpenCue_Cuebot"
        property "sonar.projectName", "OpenCue Cuebot"
        // NOTE: sonar.login must be provided manually, like:
        //   ./gradlew sonarqube -Dsonar.login=<login key>

        property "sonar.issue.ignore.multicriteria", "j1,j2"

        // Incompatible @Transactional requirements
        // It's claimed this causes runtime exceptions but we don't see such issues.
        property "sonar.issue.ignore.multicriteria.j1.ruleKey", "java:S2229"
        property "sonar.issue.ignore.multicriteria.j1.resourceKey", "**/*.java"

        // Null pointers
        // DAO classes make heavy use of getJdbcTemplate() which triggers this rule, but is
        // never null in practice.
        property "sonar.issue.ignore.multicriteria.j2.ruleKey", "java:S2259"
        property "sonar.issue.ignore.multicriteria.j2.resourceKey", "src/main/java/com/imageworks/spcue/dao/**/*.java"
    }
}

tasks.withType(AbstractArchiveTask) {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

tasks.withType(Test) {
    // Configure logging when running Gradle with --info or --debug.
    testLogging {
        info {
            // Don't show STANDARD_OUT messages, these clutter up the output
            // and make it hard to find actual failures.
            events TestLogEvent.FAILED
            exceptionFormat TestExceptionFormat.FULL
            showStandardStreams false
        }
        debug {
            // Show everything.
            events TestLogEvent.STARTED,
                   TestLogEvent.FAILED,
                   TestLogEvent.PASSED,
                   TestLogEvent.SKIPPED,
                   TestLogEvent.STANDARD_ERROR,
                   TestLogEvent.STANDARD_OUT
            exceptionFormat TestExceptionFormat.FULL
        }
   }
}
