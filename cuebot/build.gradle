apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'com.google.protobuf'
apply plugin: 'org.sonarqube'
apply plugin: 'jacoco'

sourceCompatibility = 1.8

ext {
    activemqVersion = '5.12.0'
    // This should match the version spring_boot is trying to pull in.
    springVersion = '4.2.8.RELEASE'
    springBootVersion = '1.3.8.RELEASE'
}

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.3'
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.3'
        classpath 'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.7.1'
    }
}

configurations {
    compile.exclude module: 'spring-boot-starter-logging'
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile group: 'com.google.code.gson', name: 'gson', version: '2.3.1'
    compile group: 'com.google.guava', name: 'guava', version: '12.0.1'
    compile group: 'com.sun.mail', name: 'mailapi', version: '1.5.4'
    compile group: 'commons-lang', name: 'commons-lang', version: '2.6'
    compile group: 'io.grpc', name: 'grpc-all', version: '1.14.0'
    compile group: 'org.apache.activemq', name: 'activemq-pool', version: activemqVersion
    compile group: 'org.apache.velocity', name: 'velocity', version: '1.7'
    compile group: 'org.jdom', name: 'jdom', version: '1.1.3'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-jdbc', version: springBootVersion
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-log4j', version: springBootVersion
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: springBootVersion
    compile group: 'org.springframework', name: 'spring-context-support', version: springVersion
    compile group: 'org.springframework', name: 'spring-jms', version: springVersion
    compile group: 'org.quartz-scheduler', name: 'quartz', version: '2.2.1', { exclude group: 'c3p0', module: 'c3p0' }
    compile group: 'org.postgresql', name: 'postgresql', version: '42.2.2'
    compile group: 'com.google.protobuf', name: 'protobuf-java', version: '3.4.0'

    protobuf fileTree("../proto/")

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: springBootVersion
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.8.0'
    testCompile group: 'com.opentable.components', name: 'otj-pg-embedded', version: '0.12.1'
    testCompile group: 'org.flywaydb', name: 'flyway-core', version: '5.2.0'

    compileOnly group: 'org.springframework.boot', name: 'spring-boot-starter-tomcat', version: springBootVersion
}

protobuf {
    protoc {
        // The protoc compiler
        artifact = 'com.google.protobuf:protoc:3.4.0'
    }
    plugins {
        grpc {
            // Generate gRPC stubs.
            artifact = 'io.grpc:protoc-gen-grpc-java:1.7.0'
        }
    }
    generateProtoTasks {
        ofSourceSet('main')*.plugins {
            // Apply the "grpc" plugin to all sources.
            grpc { }
        }
    }
    generatedFilesBaseDir = "${projectDir}/src/compiled_protobuf"
}

// Help IDEs recognize the generated code.
sourceSets {
    main {
        java {
            srcDirs += 'src/compiled_protobuf/main/java'
            srcDirs += 'src/compiled_protobuf/main/grpc'
        }
    }
    test {
        resources.srcDirs = ["src/test/resources"]
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'com.imageworks.spcue.CuebotApplication'
    }
}

shadowJar {
    mergeServiceFiles('META-INF/spring.*')
}

jacoco {
    toolVersion = "0.8.4"
}

jacocoTestReport {
    reports {
        // XML is used by SonarCloud. HTML is generated if you want to generate a human-readable
        // report locally. Reports are placed in build/reports/jacoco/.
        xml.enabled true
        html.enabled true
    }

    // Exclude files from test coverage.
    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it,
                     exclude: [
                        // Exclude proto files' generated Java code.
                        'com/imageworks/spcue/grpc/**',
                        // Exclude Oracle classes; our standard test environment isn't currently
                        // able to run Oracle unit tests, so coverage of those files will always
                        // read as 0% even though tests do exist.
                        'com/imageworks/spcue/dao/oracle/**',
                        'com/imageworks/spcue/dao/criteria/oracle/**',])
        })
    }
}

sonarqube {
    properties {
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.organization", "academysoftwarefoundation"
        property "sonar.projectKey", "AcademySoftwareFoundation_OpenCue_Cuebot"
        property "sonar.projectName", "OpenCue Cuebot"
        // SonarCloud will pick up the JaCoCo report automatically, but has its own options
        // for excluding files. We don't need to exclude generated code here as it isn't
        // checked into the repository, so SonarCloud doesn't even know it exists.
        property "sonar.coverage.exclusions", "src/main/java/com/imageworks/spcue/dao/oracle/**," +
                                              "src/main/java/com/imageworks/spcue/dao/criteria/oracle/**"
        // NOTE: sonar.login must be provided manually, like:
        //   ./gradlew sonarqube -Dsonar.login=<login key>
    }
}
